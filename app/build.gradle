plugins {
    id("dev.msc.application")
    id("dev.msc.hilt")
    id("kotlin-kapt")
    id("com.google.gms.google-services")
    id("com.google.firebase.crashlytics")
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

android {

    defaultConfig {
        applicationId "dev.olog.msc"

        buildConfigField 'String', "LAST_FM_KEY", localProperties.last_fm_key
        buildConfigField 'String', "LAST_FM_SECRET", localProperties.last_fm_secret
        buildConfigField 'String', "AES_PASSWORD", localProperties.aes_password
    }

    buildTypes {
        release {
            minifyEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
    }

    buildFeatures {
        buildConfig = true
    }
}


dependencies {
    implementation project(':app-shortcuts')
    implementation project(':core')
    implementation project(':data')
    implementation project(':image-provider')
    implementation project(':media')
    implementation project(':prefs-keys')
    implementation project(':presentation')
    implementation project(':shared-android')
    implementation project(':shared-widgets')
    implementation project(':service-music')
    implementation project(':service-floating')
    implementation project(':offline-lyrics')
    implementation project(':intents')

    // google architecture components
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.extensions
    implementation libs.androidx.lifecycle.java8
    implementation libs.androidx.lifecycle.viewmodel

    implementation libs.androidx.core
    implementation libs.androidx.appcompat
    implementation libs.androidx.browser
    implementation libs.androidx.preference

    implementation libs.ui.blurkit
    implementation libs.ui.customtabs

    // debug
    debugImplementation libs.utils.leakcanary
    implementation libs.utils.aescrypto

    // firebase
    implementation libs.firebase.core
    implementation libs.firebase.crashlytics
}